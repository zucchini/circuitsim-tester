// In this section you declare where to find the dependencies of your project
plugins {
    // Apply the java plugin to add support for Java
    id 'java'
    id 'java-library'

    // Produce maven packages for easy inclusion
    id 'maven-publish'

    id 'com.palantir.git-version' version "0.12.3"
}

group = 'io.zucchini.circuitsim-tester'
version gitVersion()

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

configurations {
    includedLibs
}

dependencies {
    includedLibs files('./CircuitSim.jar')
    implementation files('./CircuitSim.jar')
    api 'org.junit.jupiter:junit-jupiter-api:5.2.0'
    api 'org.junit.jupiter:junit-jupiter-params:5.2.0'
    api 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
    api 'org.junit.platform:junit-platform-launcher:1.2.0'
}

compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

jar {
    manifest {
        attributes('Main-Class': "io.zucchini.circuitsimtester.launcher.TesterLauncher")
    }

    // Include dependencies
    from { configurations.includedLibs.collect { it.isDirectory() ? it : zipTree(it) } }
}

javadoc {
    title 'CircuitSim Autograder API'
    include 'io/zucchini/circuitsimtester/api/*', 'io/zucchini/circuitsimtester/extension/*'
    // For github pages
    destinationDir = file('docs')
    options.addBooleanOption 'html5', true
    options.addStringOption  'link', 'https://docs.oracle.com/en/java/javase/11/docs/api/'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

// Create the publication with the pom configuration:
publishing {
    publications {
        MavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId project.name
            version project.version
            pom.withXml {
                asNode().dependencies.dependency.each { dep ->
                    if (dep.artifactId.last().value().last().contains("CircuitSim")) {
                        assert dep.parent().remove(dep)
                    }
                }
            }
        }
    }
    repositories {
        maven {
          name = "GitHubPackages"
          url = "https://maven.pkg.github.com/zucchini/circuitsim-tester"
          credentials {
            username = System.getenv("USERNAME")
            password = System.getenv("TOKEN")
          }
        }
    }
}
